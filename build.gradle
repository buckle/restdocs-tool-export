plugins {
  id 'org.asciidoctor.convert' version '1.5.3'
  id 'java'
  id 'jacoco'
  id 'maven-publish'
  id 'signing'
}

group = 'com.github.buckle'
version = '0.0.3-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
  mavenCentral()
}

ext {
  set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
  compile('org.springframework.restdocs:spring-restdocs-mockmvc:2.0.3.RELEASE')
  compile('org.slf4j:slf4j-api:1.7.28')

  implementation('org.apache.commons:commons-lang3:3.9')
  implementation('commons-io:commons-io:2.6')
  implementation('org.apache.commons:commons-text:1.7')
  implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.9')

  testImplementation('org.springframework.boot:spring-boot-starter-web:2.1.7.RELEASE')
  testImplementation('org.springframework.boot:spring-boot-starter-test:2.1.7.RELEASE') {
    exclude group: 'junit', module: 'junit'
  }
  testImplementation('org.mockito:mockito-core:2.27.0')
  testImplementation('org.mockito:mockito-junit-jupiter:2.27.0')
  testImplementation('org.junit.jupiter:junit-jupiter:5.4.2')
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
  archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
  from javadoc
  archiveClassifier = 'javadoc'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourceJar
      artifact javadocJar
      pom {
        name = 'Restdocs Tool Export'
        description = 'Generates AsciiDoc snippets via Spring Restdocs that are exports for Insomnia or Postman that can be download and imported.'
        url = 'https://github.com/buckle/restdocs-tool-export'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'stacyp'
            name = 'Stacy Peace'
            email = 'stacy.peace@buckle.com'
          }
        }
        scm {
          connection = 'scm:git:git@github.com:buckle/restdocs-tool-export.git'
          developerConnection = 'scm:git:git@github.com:buckle/restdocs-tool-export.git'
          url = 'https://github.com/buckle/restdocs-tool-export'
        }
      }
    }
  }
  repositories {
    maven {
      def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
      def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
      url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

      credentials {
        username = project.findProperty('mavenCentralUser') ? project.findProperty('mavenCentralUser') : ''
        password = project.findProperty('mavenCentralAPIKey') ? project.findProperty('mavenCentralAPIKey') : ''
      }
    }
  }
}

signing {
  useGpgCmd()
  sign publishing.publications.mavenJava
}

test {
  outputs.dir snippetsDir
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

jacocoTestCoverageVerification {
  dependsOn test
  violationRules {
    rule {
      element = 'CLASS'
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 1.0
      }
    }
    rule {
      element = 'CLASS'
      limit {
        counter = 'BRANCH'
        value = 'COVEREDRATIO'
        minimum = 1.0
      }
    }
  }
}

check.dependsOn jacocoTestCoverageVerification

asciidoctor {
  inputs.dir snippetsDir
  dependsOn test
}
